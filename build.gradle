plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'application'
    id 'jacoco'
}

group = 'io.github.CCSCovenant'
archivesBaseName = "JTab"
version = '1.0-snapshot'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}


jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    implementation 'org.apache.commons:commons-csv:1.8'

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.12.5"

    implementation 'com.github.JensPiegsa:jfugue:5.0.9'
    implementation 'com.jfoenix:jfoenix:9.0.10'


}

task debug {
    group = "application"
    description = "Runs this project in debug mode on port 2311"
	doFirst {
		application {
    		applicationDefaultJvmArgs = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=2311']
		}
	}
	finalizedBy run
}

test {
    useJUnitPlatform()
    testLogging {
        //events 'passed', 'skipped', 'failed'
        events 'passed', 'failed'
    }
    finalizedBy jacocoTestReport
}

task testDebug {
	group = "verification"
    description = "Runs the tests in debug mode on port 2311"
    doFirst {
    	test {
    		jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=2311'
   		}
   	}
    finalizedBy test
}


run {
	enableAssertions = true
}
task sourceJar(type:Jar){
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}
task javadocJar(type:Jar){
    from javadoc
    archiveClassifier = 'javadoc'
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.CCSCovenant'
            artifactId = 'tab2xml'
            version = "1.0-released"
            from components.java
            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }
            pom {
                name = 'tab2xml'
                description = 'A lib that convert tablature to music xml'
                url = 'https://github.com/CCSCovenant/TAB2XML-lib'
                licenses {
                    license {
                        name = ' Creative Commons Attribution-ShareAlike 4.0 International License.'
                        url = 'https://creativecommons.org/licenses/by-sa/4.0/'
                    }
                }
                developers {
                    developer {
                        id = 'ccs'
                        name = 'kuimou'
                        email = 'pzeugen@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git://github.com/CCSCovenant/TAB2XML-lib.git'
                    developerConnection = 'scm:git://github.com/CCSCovenant/TAB2XML-lib.git'
                    url = 'https://github.com/CCSCovenant/TAB2XML-lib'
                }

            }
        }
    }
    repositories {
        maven {
            credentials {
                username = "$ossrhUsername"
                password = "$ossrhPassword"
            }

            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
